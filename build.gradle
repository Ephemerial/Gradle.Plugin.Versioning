buildscript {
	repositories { mavenCentral() }
	dependencies { classpath(group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: '1.0.0-beta-4584') }
}

plugins {
	id 'com.gradle.plugin-publish' version '0.9.3'
	id 'com.zoltu.gradle.plugin.git-versioning' version '0.0.0'
}

group 'com.zoltu.gradle.plugin'

apply plugin: 'kotlin'

repositories {
	jcenter()
}

sourceSets.main.java.srcDirs = []

dependencies {
	compile(gradleApi())
	compile(group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.0.0-beta-4584')
	compile(group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.1.1.201511131810-r')
}

def gradlePublishKey = System.getenv('GRADLE_PUBLISH_KEY')
if (gradlePublishKey != null)
	System.setProperty('gradle.publish.key', gradlePublishKey)
def gradlePublishSecret = System.getenv('GRADLE_PUBLISH_SECRET')
if (gradlePublishSecret != null)
	System.setProperty('gradle.publish.secret', gradlePublishSecret)

pluginBundle {
	website = 'https://github.com/Zoltu/Gradle.Plugin.Versioning'
	vcsUrl = 'https://github.com/Zoltu/Gradle.Plugin.Versioning'

	plugins {
		gitVersioningPlugin {
			id = 'com.zoltu.gradle.plugin.git-versioning'
			displayName = 'Git Versioning'
			description = 'Automatically bumps your minor version with every git commit and gets major/minor from git tags.  Also sets version in Java manifest.'
			tags = ['version', 'versioning', 'git', 'describe', 'git describe']
		}
	}
}
